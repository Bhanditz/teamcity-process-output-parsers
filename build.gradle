buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath group: 'com.github.rodm', name: 'gradle-teamcity-plugin', version: '0.8.1'
    }
}

ext {
    teamcityVersion = project.hasProperty('teamcityVersion') ? project.getProperty('teamcityVersion') : '9.1.6'
    teamcityDir = project.hasProperty('teamcityDir') ? project.getProperty('teamcityDir') : "$rootDir/servers/TeamCity-${teamcityVersion}"
    teamcityDataDir = "$rootDir/data/" + (teamcityVersion =~ /(\d+\.\d+).*/)[0][1]
    teamcityJavaHome = System.properties['java.home']
    teamcityGeneratedPoms = "$buildDir/generated-poms"
    teamcityInstall = project.hasProperty("teamcityInstall") ? project.getProperty("teamcityInstall") : false
}

allprojects {
    group = 'org.jetbrains.teamcity.output_parser'
    version = '1.0-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repository.jetbrains.com/teamcity" }
    }
    configurations {
        provided
    }

    if (!teamcityInstall) {
        project.tasks.findByName("downloadTeamCity")*.setEnabled(false)
    }
}

subprojects {
    apply plugin: 'java'
    dependencies {
        testCompile("org.jetbrains.teamcity:tests-support:${teamcityVersion}")
    }
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"

    test {
        useTestNG()
    }

    apply plugin: 'maven-publish'
    task sourcesJar(type: Jar) {
        from sourceSets.main.allJava
    }

    task testsSourcesJar(type: Jar) {
        from sourceSets.test.allJava

    }
    task testsJar(type: Jar) {
        from sourceSets.test.output
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar {
                    classifier "sources"
                }
                artifact testsSourcesJar {
                    classifier "test-sources"
                }
                artifact testsJar {
                    classifier "test"
                }
            }
        }

        repositories {
            maven {
                url "${rootProject.buildDir}/repo"
            }
        }
    }
}

apply plugin: 'com.github.rodm.teamcity-server'
apply plugin: 'idea'
//apply from: 'export-teamcity-to-local-maven.gradle'

dependencies {
    agent project(path: ':text-translators-agent', configuration: 'plugin')
}

teamcity {
    version = teamcityVersion

//    descriptor {
//        name = project.name
//        displayName = 'Process Output Parsers'
//        version = project.version
//        vendorName = 'JetBrains'
//        description = 'Process Output Parsers'
//        useSeparateClassloader = true
//    }

    descriptor = project.file('teamcity-server-plugin.xml')

    homeDir = file(teamcityDir)
    dataDir = file(teamcityDataDir)
    javaHome = file(teamcityJavaHome)
    serverOptions = '-Xdebug -Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5500 -Dteamcity.oauthConnections.enabled=true -Dteamcity.development.mode=true -Dteamcity.development.shadowCopyClasses=true'
}


idea {
    module {
        downloadJavadoc = false
        downloadSources = true
    }
}

deployPlugin.dependsOn(build)

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}
